on init
    declare $source := 0
    declare $active_target := 0
    declare %new_note_target[16] := (0)
    declare $done := 0

    set_script_title("Shift Router")
    message("")
end on

on midi_in
    $done := 0
    $source := $MIDI_CHANNEL

    { patch change }
    if ($MIDI_COMMAND = $MIDI_COMMAND_NOTE_ON and $MIDI_BYTE_1 < 4)
        %new_note_target[$source] := $MIDI_BYTE_1
        message("Switched channel " & $source & " to patch " & $MIDI_BYTE_1)
        $done := 1
    end if
    if (($MIDI_COMMAND = $MIDI_COMMAND_NOTE_OFF or ...
         $MIDI_COMMAND = $MIDI_COMMAND_NOTE_ON and $MIDI_BYTE_2 = 0) ...
         and $MIDI_BYTE_1 < 4)
        %new_note_target[$source] := 0
        message("Switch for channel " & $source & " released")
        $done := 1
    end if

    { note on }
    if ($done = 0 and $MIDI_COMMAND = $MIDI_COMMAND_NOTE_ON and $MIDI_BYTE_2 > 0)
        $active_target := %new_note_target[$source] * 16 + $source
        set_midi($active_target , $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
    end if

    { note off }
    if ($done = 0 and ($MIDI_COMMAND = $MIDI_COMMAND_NOTE_OFF or ...
           $MIDI_COMMAND = $MIDI_COMMAND_NOTE_ON and $MIDI_BYTE_2 = 0))
      set_midi($source, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 16, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 32, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 48, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
    end if

    if ($MIDI_COMMAND = $MIDI_COMMAND_CC)
      set_midi($source, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 16, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 32, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
      set_midi($source + 48, $MIDI_COMMAND, $MIDI_BYTE_1, $MIDI_BYTE_2)
    end if

    ignore_midi
end on
